apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'signing'
apply plugin: 'maven'
//apply plugin: 'nebula.lint'

sourceCompatibility = 1.8
version = "${version}"
group = 'com.dell.isg.smi'
archivesBaseName = "commons-elm"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
   repositories {
    mavenLocal()
	maven {
		url "${artifactory_contextUrl}/${plugin_repo}"
		url "https://plugins.gradle.org/m2/"
	}
  }
  dependencies {
       classpath "io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE"
       classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.15"
       classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
       classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
       //classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
  }
}

//gradleLint {
  //rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

sonarqube {
    properties {
       property "sonar.projectName", "rackhd :: smi-lib-commons-elm"
       property "sonar.projectKey", "${group}:commons-elm"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'commons-elm jar',
                   'Implementation-Version': version
    }
}

javadoc {
    options {
         encoding = 'windows-1251'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

repositories {
	mavenLocal()
	maven {
	    url "${artifactory_contextUrl}/${dependency_repo}"
	}
}

ext {
	springVersion = '4.3.6.RELEASE'
}


dependencies {
	compile "org.springframework:spring-web:${springVersion}"
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	compile group: 'javax.persistence', name: 'persistence-api', version: '1.0.2'
	compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.8'
	compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.25.1'
	compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.25.1'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("artifactoryPublish") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifact jar
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar
        }
    }
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        defaults{ publications ("mavenJava")}
        repository {
	    // note: username and password being set by bamboo artifactory plugin
            maven = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
        }
    }
}

task writeNewPom {
    doLast {
        pom {
            artifactId = "commons-elm"
            project {
                inceptionYear '2017'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/pom.xml")
    }
}